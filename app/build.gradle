apply plugin: 'com.android.application'
def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    dataBinding{
        enabled true
    }
    defaultConfig {
        applicationId "com.hloong.newtech"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // AndroidManifest.xml 里面UMENG_CHANNEL的value为 ${UMENG_CHANNEL}
        manifestPlaceholders = [UMENG_CHANNEL_VALUE: "umeng"]//添加一个默认渠道号
    }

    //添加我们签名文件配置
    signingConfigs {
        debug {
            storeFile file("hloong.jks")
            storePassword "hloongsky1210"
            keyAlias "hloong"
            keyPassword "hloongsky1210"
        }
        release {
            storeFile file("hloong.jks")
            storePassword "hloongsky1210"
            keyAlias "hloong"
            keyPassword "hloongsky1210"
        }
    }

    buildTypes {
        debug {
            shrinkResources false // 移除无用的resource文件
            minifyEnabled false //不启用混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                    'proguard-fresco.pro'
            //签名配置
            signingConfig signingConfigs.debug
        }
        release {
            shrinkResources false // 移除无用的resource文件
            minifyEnabled false //不启用混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名配置
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def outputFile = output.outputFile
                    def fileName
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为XXapp1.4.0_2017-02-14.apk
                        if (variant.buildType.name.equals('release')) {
                            fileName = "qkapp${defaultConfig.versionName}_${variant.productFlavors[0].name}_${releaseTime()}.apk"
                        } else if (variant.buildType.name.equals('debug')) {
                            fileName = "qkapp${defaultConfig.versionName}_${releaseTime()}_debug.apk"
                        }
                        output.outputFile = new File(outputFile.parent, fileName)
                    }
                }
            }
        }
    }

    productFlavors{
        Alpha {//自己打包测试使用
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Alpha]
        }
        Tencent {//投放应用宝市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Tencent]
        }
        Baidu {//投放百度市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Baidu]
        }
        Wandoujia {//投放豌豆荚市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Wandoujia]
        }
        PP {//投放PP助手市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: PP]
        }
        Qihoo {//投放Qihoo市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Qihoo]
        }
        Vivo {//投放vivo市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Vivo]
        }
        Oppo {//投放oppo市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Oppo]
        }
        Xiaomi {//投放小米市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Xiaomi]
        }
        Meizu {//投放魅族市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Meizu]
        }
        Huawei {//投放华为应用市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Huawei]
        }
        Lenovo {//投放联想市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Lenovo]
        }
        Letv {//投放乐视市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Letv]
        }
        Gionee {//投放金立市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: Gionee]
        }
        HiMarket {//投放安卓市场
            //     manifestPlaceholders = [UMENG_CHANNEL_VALUE: HiMarket]
        }

    }

    productFlavors.all{
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.umeng.analytics:analytics:latest.integration'//umeng统计
    testCompile 'junit:junit:4.12'
}
